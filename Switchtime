#!/usr/bin/env perl
use Mojolicious::Lite;

my $status_for_switch = {};

my $clients = {};

# Template with browser-side code
get '/' => sub {
  my $c = shift;
  $c->stash( qty => 9 ); # scalar keys $status_for_switch );
  $c->render('index');
};

# WebSocket echo service
websocket '/echo' => sub {
  my $c = shift;
  app->log->debug( sprintf 'Client connected: %s', $c->tx );
  my $id = sprintf "%s", $c->tx;
  $clients->{$id} = $c->tx;

  # Opened
  $c->app->log->debug('WebSocket opened');

  # Allow inactivity indefinitely
  $c->inactivity_timeout(0);

  # Incoming message
  $c->on(message => sub {
    my ($c, $msg) = @_;
    # $c->send("echo: $msg");
    $c->app->log->debug("incoming: $msg");
    if ( $msg ne 'init' ){
      toggle_switch(msg => $msg);
    }

    my $num_clients = scalar keys %$clients;
    for (keys %$clients) {
        $clients->{$_}->send({json => {
            statuses => $status_for_switch,
            num_clients => $num_clients,
        }});
    }
  });

  # Closed
  $c->on(finish => sub {
    my ($c, $code, $reason) = @_;
    $c->app->log->debug("WebSocket closed with status $code");
    delete $clients->{$id};
    update_clients( clients => $clients );
  });
};

sub update_clients {
  my (%args) = @_;
  my $clients = $args{clients};

  my $num_clients = scalar keys %$clients;
  for (keys %$clients) {
      $clients->{$_}->send({json => {
          statuses => $status_for_switch,
          num_clients => $num_clients,
      }});
  }
  return;
}

sub toggle_switch {
  my (%args) = @_;
  if ( $status_for_switch->{$args{msg}} eq 'on' ){
    $status_for_switch->{$args{msg}} = 'off';
  }
  else {
    $status_for_switch->{$args{msg}} = 'on';
  }

  return;
}

app->start;
__DATA__

@@ index.html.ep
<!DOCTYPE html>
<html>
  <head><title>Swit.chti.me</title></head>
  <body>
    <script>
      var ws = new WebSocket('<%= url_for('echo')->to_abs %>');

      // Incoming messages
      ws.onmessage = function (event) {
        var res = JSON.parse(event.data);
        for ( var s in res.statuses ) {
          document.getElementById(s).value = res.statuses[s];
        }
        document.getElementById('num_clients').innerHTML = res.num_clients;
      };

      // Outgoing messages
      ws.onopen = function (event) {
        ws.send('init');
      };

      function toggleme (num) {
        ws.send( num );
      }
    </script>
    <h4>Switches:</h4>
    <%  for my $id ( 1 .. $qty ) { %>
      <input
        id='<%= $id %>'
        type='submit'
        value='<%= $id %>'
        onclick='toggleme("<%= $id %>");'
      />
    <% } %>
    <h4>Number of players currently connected:</h4>
    <p id='num_clients'>0</p>
  </body>
</html>
