#!/usr/bin/env perl
use Mojolicious::Lite;

my $num_switches = 9;
my $status_for_switch = {};

my $clients = {};

my $ghosts = {
  alice => {
    location => 5,
  },
  bob => {
    location => 7,
  },
  carol => {
    location => 1,
  },
};

my $winning = 0;

my $seconds_per_turn = 20;

Mojo::IOLoop->recurring( $seconds_per_turn => sub {
  app->ua->get('http://mojolicious.org' => sub {
    my ($ua, $tx) = @_;
    npc_turn();
  });
});

# Template with browser-side code
get '/' => sub {
  my $c = shift;
  $c->stash( qty => $num_switches );
  $c->render('index');
};

# WebSocket echo service
websocket '/echo' => sub {
  my $c = shift;
  app->log->debug( sprintf 'Client connected: %s', $c->tx );
  my $id = sprintf "%s", $c->tx;
  $clients->{$id} = $c->tx;

  # Opened
  $c->app->log->debug('WebSocket opened');

  # Allow inactivity indefinitely
  $c->inactivity_timeout(0);

  # Incoming message
  $c->on(message => sub {
    my ($c, $msg) = @_;
    $c->app->log->debug("incoming: $msg");
    if ( $msg ne 'init' ){
      toggle_switch(switch => $msg);
    }

    update_clients( clients => $clients );
  });

  # Closed
  $c->on(finish => sub {
    my ($c, $code, $reason) = @_;
    $c->app->log->debug("WebSocket closed with status $code");
    delete $clients->{$id};
    update_clients( clients => $clients );
  });
};

sub update_clients {
  my (%args) = @_;
  # global variable could be overridden if you want to just update some clients?
  my $clients = $args{clients};

  $winning = is_winning();
  my $num_clients = scalar keys %$clients;
  my $num_ghosts = scalar keys %$ghosts;
  for (keys %$clients) {
      $clients->{$_}->send({json => {
          statuses => $status_for_switch,
          num_clients => $num_clients,
          num_ghosts => $num_ghosts,
          winning => $winning,
      }});
  }

  return;
}

sub toggle_switch {
  my (%args) = @_;
  if ( $status_for_switch->{$args{switch}} && $status_for_switch->{$args{switch}} eq 'on' ){
    $status_for_switch->{$args{switch}} = 'off';
  }
  else {
    $status_for_switch->{$args{switch}} = 'on';
  }

  return;
}

sub is_winning {
  my (%args) = @_;
  my $is_winning = 0;
  my $num_clients = scalar keys %$clients;

  return 'nope' if !$num_clients;

  $is_winning = ( ( scalar map { $_ eq 'on' ? $_ : () } values %$status_for_switch ) % $num_clients ) ? 'nope' : 'yep';

  return $is_winning;
}

sub npc_turn {
    app->log->debug('tick');
    # each ghost toggles the switch at their location,
    # then moves randomly 1 or 0 spaces
    for my $ghost ( keys %$ghosts ) {
        toggle_switch( switch => $ghosts->{$ghost}->{location} );
        $ghosts->{$ghost}->{location} =
          ( ( $ghosts->{$ghost}->{location} + ( ( int rand(3) ) - 1 ) )
            % $num_switches ) + 1;
        app->log->debug( "\t"
              . $ghost
              . "'s location is: \t"
              . $ghosts->{$ghost}->{location} );
    }
    update_clients( clients => $clients );
}

app->start;
__DATA__

@@ index.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title>swit.chti.me</title>
    <link rel='icon' href='/up.png' />
  </head>
  <body>
    <script>
      var ws = new WebSocket('<%= url_for('echo')->to_abs %>');

      // Incoming messages
      ws.onmessage = function (event) {
        var res = JSON.parse(event.data);
        for ( var s in res.statuses ) {
          document.getElementById(s).alt = res.statuses[s];
          if ( res.statuses[s] == 'on' ) {
            document.getElementById(s).src = '/up.png';
          }
          else {
            document.getElementById(s).src = '/down.png';
          }
        }
        document.getElementById('num_clients').innerHTML = res.num_clients;
        document.getElementById('num_ghosts').innerHTML = res.num_ghosts;
        document.getElementById('logo').style.color = ( res.winning == 'yep' ? 'green' : 'red');
      };

      // Outgoing messages
      ws.onopen = function (event) {
        ws.send('init');
      };

      // Detect connect close
      ws.onclose = function (event) {
        document.getElementById('num_clients').innerHTML = 'lost connection <a href="javascript:window.location.href=window.location.href">reload</a>';
      };

      function toggleme (num) {
        ws.send( num );
      }
    </script>
    <pre id='logo' >
                  _ _         _     _   _
     _____      _(_) |_   ___| |__ | |_(_)  _ __ ___   ___
    / __\ \ /\ / / | __| / __| '_ \| __| | | '_ ` _ \ / _ \ 
    \__ \\ V  V /| | |_ | (__| | | | |_| |_| | | | | |  __/
    |___/ \_/\_/ |_|\__(_)___|_| |_|\__|_(_)_| |_| |_|\___|
    </pre>
    <%  for my $id ( 1 .. $qty ) { %>
      <a
        href='javascript:;'
        onclick='toggleme("<%= $id %>");'
        ><img
        id='<%= $id %>'
        type='image'
        src='/down.png'
        alt='<%= $id %>'
      /></a>
    <% } %>
    <p>
      <img src='/players.png' style='vertical-align:middle;'/>&nbsp;:
      <span id='num_clients'>0</span>
      <br/>
      <img src='./ghosts.png' style='vertical-align:middle;' />&nbsp;:
      <span id='num_ghosts'>0</span>
    </p>
  </body>
</html>
