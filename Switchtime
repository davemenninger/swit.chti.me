#!/usr/bin/env perl
use Mojolicious::Lite;
use Acme::MetaSyntactic qw( norse_mythology );
use SVG::Sparkline;

my @ghost_population = ( 3 );
my $ghost_svg_xml = '';

my $num_switches = 99;
my $status_for_switch = {};
for my $s ( 1 .. $num_switches )
{
  $status_for_switch->{$s} = 'on';
}

my $clients = {};

my $ghosts = {
  alice => {
    location => 3,
    age => 1,
  },
  bob => {
    location => 72,
    age => 109,
  },
  carol => {
    location => 96,
    age => 64,
  },
};

my $meta = Acme::MetaSyntactic->new( category => ':all' );

my $winning = 0;

my $seconds_per_turn = 15;

Mojo::IOLoop->recurring( $seconds_per_turn => sub {
    npc_turn();
});

# Template with browser-side code
get '/' => sub {
  my $c = shift;
  $c->stash( qty => $num_switches );
  $c->stash( ghost_svg => $ghost_svg_xml );
  $c->render('index');
};

# WebSocket echo service
websocket '/switches' => sub {
  my $c = shift;
  app->log->debug( sprintf 'Client connected: %s', $c->tx );
  my $id = sprintf "%s", $c->tx;
  $clients->{$id} = $c->tx;

  # Opened
  $c->app->log->debug('WebSocket opened');

  # Allow inactivity indefinitely
  $c->inactivity_timeout(0);

  # Incoming message
  $c->on(message => sub {
    my ($c, $msg) = @_;
    $c->app->log->debug("incoming: $msg");
    if ( $msg ne 'init' ){
      toggle_switch(switch => $msg);
    }

    update_clients( clients => $clients );
  });

  # Closed
  $c->on(finish => sub {
    my ($c, $code, $reason) = @_;
    $c->app->log->debug("WebSocket closed with status $code");
    delete $clients->{$id};
    update_clients( clients => $clients );
  });
};

sub update_clients {
  my (%args) = @_;
  # global variable could be overridden if you want to just update some clients?
  my $clients = $args{clients};

  $winning = is_winning();
  my $num_clients = scalar keys %$clients;
  my $num_ghosts = scalar keys %$ghosts;
  for (keys %$clients) {
      $clients->{$_}->send({json => {
          statuses => $status_for_switch,
          num_clients => $num_clients,
          num_ghosts => $num_ghosts,
          winning => $winning,
          ghost_svg => $ghost_svg_xml,
      }});
  }

  return;
}

sub toggle_switch {
  my (%args) = @_;
  if ( $status_for_switch->{$args{switch}} && $status_for_switch->{$args{switch}} eq 'on' ){
    $status_for_switch->{$args{switch}} = 'off';
  }
  else {
    $status_for_switch->{$args{switch}} = 'on';
  }

  return;
}

sub is_winning {
  my (%args) = @_;
  my $is_winning = 0;
  my $num_clients = scalar keys %$clients;

  return 'nope' if !$num_clients;

  $is_winning = ( ( scalar map { $_ eq 'on' ? $_ : () } values %$status_for_switch ) % $num_clients ) ? 'nope' : 'yep';

  return $is_winning;
}

sub npc_turn {
    app->log->debug('TICK');

    if ( rand(100) < 5 ) {
      spawn_ghost();
    }

    # each ghost toggles the switch at their location,
    # then moves randomly 1 or 0 spaces
    # then ages, and mpossibly dies of old age
    for my $ghost ( keys %$ghosts ) {
        app->log->debug( "\t"
              . $ghost . ":\t"
              . "location: \t"
              . $ghosts->{$ghost}->{location} . "\t"
              . "age: \t"
              . $ghosts->{$ghost}->{age} );
        toggle_switch( switch => $ghosts->{$ghost}->{location} );
        move_ghost( ghost => $ghost );
        age_ghost( ghost => $ghost );
        if ( $ghosts->{$ghost} ) {
            app->log->debug( "\t"
                  . $ghost . ":\t"
                  . "location: \t"
                  . $ghosts->{$ghost}->{location} . "\t"
                  . "age: \t"
                  . $ghosts->{$ghost}->{age} );
        }
    }

    $ghost_svg_xml = update_ghost_svg();
    update_clients( clients => $clients );
}

sub move_ghost {
    my (%args) = @_;

    my $move_direction   = ( int rand(3) ) - 1;                     # -1,0,1
    my $current_location = $ghosts->{ $args{ghost} }->{location};
    my $new_location     = $current_location + $move_direction;
    $new_location = 1             if $new_location > $num_switches;
    $new_location = $num_switches if $new_location < 1;
    $ghosts->{ $args{ghost} }->{location} = $new_location;
    return;
}

sub spawn_ghost {
  my $name = $meta->name(); # 'ghost'.(int rand(1000) );
  $ghosts->{$name}->{location} = (int rand($num_switches-1))+1;
  $ghosts->{$name}->{age} = 1;
  app->log->debug( "\t".$name . ' spawned at ' . $ghosts->{$name}->{location} );
  return;
}

sub age_ghost {
    my (%args) = @_;
    $ghosts->{ $args{ghost} }->{age} += 1;
    if ( $ghosts->{ $args{ghost} }->{age} > 111 )
    {
        app->log->debug( "\t" . $args{ghost} . ' died of old age!' );
        delete($ghosts->{ $args{ghost} });
    }
    return;
}

sub update_ghost_svg {
  my $num_ghosts = scalar keys %$ghosts;
  push @ghost_population , $num_ghosts;
  shift @ghost_population if ( scalar @ghost_population > 300 );
  my $sl1 = SVG::Sparkline->new( Line => { values => \@ghost_population, color => 'blue', height => 32 } );
  return $sl1->to_string;
}

app->start;
__DATA__

@@ index.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title>swit.chti.me</title>
    <link rel='icon' href='/up.png' />
    <!-- <link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/normalize/4.1.1/normalize.css' /> -->
  </head>
  <body>
    <script>
      var ws = new WebSocket('<%= url_for('switches')->to_abs %>');

      // Incoming messages
      ws.onmessage = function (event) {
        var res = JSON.parse(event.data);
        for ( var s in res.statuses ) {
          document.getElementById(s).alt = res.statuses[s];
          if ( res.statuses[s] == 'on' ) {
            document.getElementById(s).src = '/up.png';
          }
          else {
            document.getElementById(s).src = '/down.png';
          }
        }
        document.getElementById('num_clients').innerHTML = res.num_clients;
        document.getElementById('num_ghosts').innerHTML = res.num_ghosts;
        document.getElementById('logo').style.color = ( res.winning == 'yep' ? 'green' : 'red');
        document.getElementById('spark').innerHTML = res.ghost_svg;
      };

      // Outgoing messages
      ws.onopen = function (event) {
        ws.send('init');
      };

      // Detect connect close
      ws.onclose = function (event) {
        document.getElementById('num_clients').innerHTML = 'lost connection <a href="javascript:window.location.href=window.location.href">reload</a>';
      };

      function toggleme (num) {
        ws.send( num );
      }
    </script>
    <pre id='logo' >
                  _ _         _     _   _
     _____      _(_) |_   ___| |__ | |_(_)  _ __ ___   ___
    / __\ \ /\ / / | __| / __| '_ \| __| | | '_ ` _ \ / _ \ 
    \__ \\ V  V /| | |_ | (__| | | | |_| |_| | | | | |  __/
    |___/ \_/\_/ |_|\__(_)___|_| |_|\__|_(_)_| |_| |_|\___|
    </pre>
    <p>
      <img src='/players.png' style='vertical-align:middle;'/>&nbsp;:
      <span id='num_clients'>0</span>&nbsp;
      <img src='./ghosts.png' style='vertical-align:middle;' />&nbsp;:
      <span id='num_ghosts'>0</span>
    </p>
    <%  for my $id ( 1 .. $qty ) { %>
      <a
        href='javascript:;'
        onclick='toggleme("<%= $id %>");'
        ><img
        id='<%= $id %>'
        type='image'
        src='/down.png'
        alt='<%= $id %>'
        style='vertical-align:middle;'
      /></a>
    <% } %>
    <p id='spark'>
    <%== $ghost_svg %>
    </p>
  </body>
</html>
